import com.github.jk1.license.filter.LicenseBundleNormalizer
import com.github.jk1.license.render.InventoryMarkdownReportRenderer
import edu.sc.seis.launch4j.tasks.Launch4jLibraryTask

import java.util.stream.Collectors

buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.6.8")
    }
}

plugins {
    id "com.github.kkdad.dependency-license-report" version "1.16.6"
}

//apply plugin: "distribution"
apply plugin: "application"
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

licenseReport {
    // Set output directory for the report data.
    // Defaults to ${project.buildDir}/reports/dependency-license.
//    outputDir = "$projectDir/build/licenses"

    // Select projects to examine for dependencies.
    // Defaults to current project and all its subprojects
//    projects = [project] + project.subprojects

    // Adjust the configurations to fetch dependencies, e.g. for Android projects. Default is 'runtimeClasspath'
//    configurations = ['implementation']
    // Use 'ALL' to dynamically resolve all configurations:
    // configurations = ALL

    // List the groups ids to exclude from dependency report. Supports regular expressions.
    // For finer granularity, see: excludes.
    excludeGroups = ['cpdetector', 'cplex', 'com.gurobi', 'de.unijena.bioinf.ms', 'de.unijena.bioinf.cbc-java', 'de.unijena.bioinf', 'de.unijena.bioinf.utils']

    // List the ids (in module:name format) to exclude from dependency report. Supports regular expressions.
    // By default excludes is empty.
//    excludes = ['moduleGroup:moduleName']

    // Don't include artifacts of project's own group into the report
    excludeOwnGroup = true

    // Set custom report renderer, implementing ReportRenderer.
    // Yes, you can write your own to support any format necessary.
//    renderers = [new XmlReportRenderer('third-party-libs.xml', 'Back-End Libraries')]
    renderers = [/*new InventoryHtmlReportRenderer(), */ new InventoryMarkdownReportRenderer('licenses.md', "SIRIUS license information", new File(rootDir, "license-overrides.txt"))]

    // Set importers to import any external dependency information, i.e. from npm.
    // Custom importer should implement DependencyDataImporter interface.
//    importers = [new XmlReportImporter('Frontend dependencies', file(frontend_libs.xml))]

    // This is for the allowed-licenses-file in checkLicense Task
    // Accepts File, URL or String path to local or remote file
//    allowedLicensesFile = new File("$projectDir/config/allowed-licenses.json")
    filters = [new LicenseBundleNormalizer(bundlePath: "$rootDir/licence-groups.json")]
}

dependencies {
    implementation project(':sirius_rest_service')
    for (m in javaFX_modules) // add (native) javaFX dependencies to build
        implementation "org.openjfx:$m:$targetCompatibility.+:${siriusTargetPlatform.jfxClassifier().get()}"
}

mainClassName = 'org.springframework.boot.loader.JarLauncher'
//mainClassName = "de.unijena.bioinf.ms.frontend.SiriusGUIApplication"

application {
    mainClass = project.mainClassName
    applicationDefaultJvmArgs = ['-Xms1G',
                                 '-XX:MaxRAMPercentage=85',
                                 '-XX:+UseG1GC',
                                 '-XX:+UseStringDeduplication',
                                 '-XX:+ExitOnOutOfMemoryError']
    executableDir = 'bin'
    applicationName = 'sirius'
}


launch4j {
    mainClassName = project.mainClassName
    libraryDir = libDir == null ? "lib" : libDir
//    jar = getLibraryDir() + "\\" + getRootProject().getChildProjects().get('sirius_gui').tasks.getByName('jar').outputs.files.singleFile.getName()
    jar = getLibraryDir() + "\\" + getRootProject().getChildProjects().get('sirius_rest_service').tasks.getByName('bootJar').outputs.files.singleFile.getName()
    jvmOptions = application.applicationDefaultJvmArgs
    dontWrapJar = true
    bundledJrePath = launch4jJDKPath != null ? launch4jJDKPath : "../runtime"
//    bundledJre64Bit = true
    classpath = [(libDir == null ? "lib" : libDir) + "\\*",
                 "%GUROBI_HOME%\\lib\\gurobi.jar",
                 "%CPLEX_HOME%\\lib\\cplex.jar"
    ]
    jdkPreference = "preferJdk"
    jreMinVersion = null
    jreMaxVersion = null
}

//additional GUI win launcher
task createGuiExe(type: Launch4jLibraryTask) {
    outfile = 'sirius-gui.exe'
    fileDescription = 'SIRIUS graphical user interface'
    headerType = "gui"
    cmdLine = "gui"
}
createGuiExe.dependsOn 'launch4j'


startScripts {
    applicationName = "sirius"
    defaultJvmOpts = application.applicationDefaultJvmArgs

    doLast {
        // writer sh script
        if (!siriusTargetPlatform.isWin()) {
            def templateBinding = [
                    toolName : "SIRIUS",
                    mainClass: mainClassName
            ]
            def sf = file("${rootDir}/scripts/sirius.sh.templ")
            templateBinding.shell = "sh"
            templateBinding.classPath = "\\\"\$JAR_HOME/*\\\":\\\"\$GUROBI_HOME/lib/gurobi.jar\\\":\\\"\$CPLEX_HOME/lib/cplex.jar\\\""
            templateBinding.javaLibPath = ""
            templateBinding.gurobiHome = "/lib"
            templateBinding.cplexHome = "/bin/x86-64_linux"
            templateBinding.jvmOpts = "${application.applicationDefaultJvmArgs.stream().collect(Collectors.joining(' '))}"
            templateBinding.javaCommand = "\$APP_HOME/runtime/bin/java"
            templateBinding.jarHome = libDir == null ? "\$APP_HOME/lib" : libDir

            String script = sf.text
            templateBinding.forEach({ k, v -> script = script.replace("#$k#", v) })
            unixScript.withWriter { it.write(script) }
            unixScript.setExecutable(true, false)
        } else {
            delete(unixScript)
        }

        if (siriusTargetPlatform.isWin()) {
            //reset binding and create windows bat
            def templateBinding = [
                    toolName : "SIRIUS",
                    mainClass: mainClassName
            ]
            // write bat
            def sf = file("${rootDir}/scripts/sirius.bat.templ")
            templateBinding.classPath = "%JAR_HOME%\\*"
            templateBinding.jvmOpts = "${application.applicationDefaultJvmArgs.stream().map({ s -> '"' + s + '"' }).collect(Collectors.joining(" "))}"
            templateBinding.jarHome = libDir == null ? "\$APP_HOME/lib" : libDir
            templateBinding.gurobiJar = "%GUROBI_HOME%\\lib\\gurobi.jar"
            templateBinding.cplexJar = "%CPLEX_HOME%\\lib\\cplex.jar"

            String script = sf.text
            templateBinding.forEach({ k, v -> script = script.replace("#$k#", v) })
            windowsScript.withWriter { it.write(script) }
            windowsScript.setExecutable(true, false)
        } else {
            delete(windowsScript)
        }
    }
}

distributions {
    sirius {
        contents {
            into('lib') {
                from("${getRootProject().getChildProjects().get('sirius_rest_service').tasks.getByName('bootJar').outputs.files.singleFile.getParent()}")
            }
            into('bin') {
                from startScripts
                if (siriusTargetPlatform.isWin()) {
                    from("${buildDir}/launch4j") {
                        include("*.exe")
                    }
                }
            }
            from("${rootDir}") {
                include('COPYING.txt')
                include('LICENSE.txt')
            }
            from("${buildDir}/reports/dependency-license") {
                include('licenses.md')
            }
            if (includeJreFX){
                into('runtime'){
                    from getParent().tasks.getByName('extractRuntimeImageFX')
                }
            }
        }
    }
}

installSiriusDist.dependsOn getRootProject().getChildProjects().get('sirius_rest_service').tasks.getByName('bootJar')
siriusDistZip.dependsOn 'generateLicenseReport'
siriusDistZip.dependsOn('launch4j')
siriusDistZip.dependsOn('createGuiExe')
siriusDistTar.dependsOn('launch4j')
siriusDistTar.dependsOn 'generateLicenseReport'
siriusDistTar.dependsOn('createGuiExe')
installSiriusDist.dependsOn('launch4j')
installSiriusDist.dependsOn('createGuiExe')
installSiriusDist.dependsOn('generateLicenseReport')
if (includeJreFX)
    installSiriusDist.dependsOn getParent().tasks.getByName('extractRuntimeImageFX')
distribution.dependsOn 'siriusDistZip'
//distribution.dependsOn 'siriusDistTar'
distribution.dependsOn 'createChecksums'
import com.github.jk1.license.filter.LicenseBundleNormalizer
import com.github.jk1.license.render.InventoryMarkdownReportRenderer
import edu.sc.seis.launch4j.tasks.Launch4jLibraryTask
import com.github.jk1.license.filter.LicenseBundleNormalizer
import com.github.jk1.license.render.*

import java.util.stream.Collectors

plugins {
    id "com.github.kkdad.dependency-license-report" version "1.16.6"
}


apply plugin: "application"

ext {
    libDir = findProperty("build.sirius.location.lib")
    includeWinStarter = !findProperty("build.sirius.starter.remove.win")
    includeIxStarter = !findProperty("build.sirius.starter.remove.ix")
    launch4jJDK = findProperty("build.sirius.starter.jdk.win")
    excludeNativeWin = findProperty("build.sirius.native.remove.win")
    excludeNativeLinux = findProperty("build.sirius.native.remove.linux")
    excludeNativeMac = findProperty("build.sirius.native.remove.mac")
}

licenseReport {
    // Set output directory for the report data.
    // Defaults to ${project.buildDir}/reports/dependency-license.
//    outputDir = "$projectDir/build/licenses"

    // Select projects to examine for dependencies.
    // Defaults to current project and all its subprojects
//    projects = [project] + project.subprojects

    // Adjust the configurations to fetch dependencies, e.g. for Android projects. Default is 'runtimeClasspath'
//    configurations = ['implementation']
    // Use 'ALL' to dynamically resolve all configurations:
    // configurations = ALL

    // List the groups ids to exclude from dependency report. Supports regular expressions.
    // For finer granularity, see: excludes.
    excludeGroups = ['cpdetector', 'cplex', 'com.gurobi', 'de.unijena.bioinf.ms', 'de.unijena.bioinf.cbc-java', 'de.unijena.bioinf', 'de.unijena.bioinf.utils']

    // List the ids (in module:name format) to exclude from dependency report. Supports regular expressions.
    // By default excludes is empty.
//    excludes = ['moduleGroup:moduleName']

    // Don't include artifacts of project's own group into the report
    excludeOwnGroup = true

    // Set custom report renderer, implementing ReportRenderer.
    // Yes, you can write your own to support any format necessary.
//    renderers = [new XmlReportRenderer('third-party-libs.xml', 'Back-End Libraries')]
    renderers = [/*new InventoryHtmlReportRenderer(), */new InventoryMarkdownReportRenderer('licenses.md', "SIRIUS license information", new File(rootDir, "license-overrides.txt"))]

    // Set importers to import any external dependency information, i.e. from npm.
    // Custom importer should implement DependencyDataImporter interface.
//    importers = [new XmlReportImporter('Frontend dependencies', file(frontend_libs.xml))]

    // This is for the allowed-licenses-file in checkLicense Task
    // Accepts File, URL or String path to local or remote file
//    allowedLicensesFile = new File("$projectDir/config/allowed-licenses.json")
    filters = [new LicenseBundleNormalizer(bundlePath: "$rootDir/licence-groups.json")]
}

dependencies {
    implementation project(':sirius_gui')
    implementation group: 'org.slf4j', name: 'slf4j-jdk14', version: "$slf4j_version"
}
mainClassName = "de.unijena.bioinf.ms.frontend.SiriusGUIApplication"

application {
    mainClass = project.mainClassName
    applicationDefaultJvmArgs = ['-Xms1G',
                                 '-XX:MaxRAMPercentage=85',
                                 '-XX:+UseG1GC',
                                 '-XX:+UseStringDeduplication',
                                 '-XX:+ExitOnOutOfMemoryError']
    executableDir = 'bin'
    application.applicationName = 'sirius'
}


createExe {
    mainClassName = project.mainClassName
    libraryDir = libDir == null ? "lib" : libDir
    jar = getLibraryDir() + "\\" + getRootProject().getChildProjects().get('sirius_gui').tasks.getByName('jar').outputs.files.singleFile.getName()
    jvmOptions = application.applicationDefaultJvmArgs
    dontWrapJar = true
    bundledJrePath = launch4jJDK
    bundledJre64Bit = true
    classpath = [(libDir == null ? "lib" : libDir) + "\\*",
                 "%GUROBI_HOME%\\lib\\gurobi.jar",
                 "%CPLEX_HOME%\\lib\\cplex.jar"
    ]
    jdkPreference = "preferJdk"
    jreMinVersion = null
    jreMaxVersion = null
}

//additional GUI win launcher
task createGuiExe(type: Launch4jLibraryTask) {
    outfile = 'sirius-gui.exe'
    fileDescription = 'SIRIUS graphical user interface'
    headerType = "gui"
    cmdLine = "gui"
}


startScripts {
    applicationName = "sirius"
    defaultJvmOpts = application.applicationDefaultJvmArgs

    doLast {
        // writer sh script
        if (includeIxStarter) {
            def templateBinding = [
                    toolName : "SIRIUS",
                    mainClass: mainClassName
            ]
            def sf = file("${rootDir}/scripts/sirius.sh.templ")
            templateBinding.shell = "sh"
            templateBinding.classPath = "\\\"\$JAR_HOME/*\\\":\\\"\$GUROBI_HOME/lib/gurobi.jar\\\":\\\"\$CPLEX_HOME/lib/cplex.jar\\\""
            templateBinding.javaLibPath = ""
            templateBinding.gurobiHome = "/lib"
            templateBinding.cplexHome = "/bin/x86-64_linux"
            templateBinding.jvmOpts = "${application.applicationDefaultJvmArgs.stream().collect(Collectors.joining(' '))}"
            templateBinding.javaCommand = "\$APP_HOME/lib/runtime/bin/java"
            templateBinding.jarHome = libDir == null ? "\$APP_HOME/lib" : libDir

            String script = sf.text
            templateBinding.forEach({ k, v -> script = script.replace("#$k#", v) })
            unixScript.withWriter { it.write(script) }
            unixScript.setExecutable(true, false)
            unixScript.renameTo(unixScript.path + '.sh')
        } else{
            delete(unixScript)
        }

        if (includeWinStarter) {
//reset binding and create windows bat
            def templateBinding = [
                    toolName : "SIRIUS",
                    mainClass: mainClassName
            ]
            // write bat
            def sf = file("${rootDir}/scripts/sirius.bat.templ")
            templateBinding.classPath = "%JAR_HOME%\\*"
            templateBinding.jvmOpts = "${application.applicationDefaultJvmArgs.stream().map({ s -> '"' + s + '"' }).collect(Collectors.joining(" "))}"
            templateBinding.jarHome = libDir == null ? "\$APP_HOME/lib" : libDir
            templateBinding.gurobiJar = "%GUROBI_HOME%\\lib\\gurobi.jar"
            templateBinding.cplexJar = "%CPLEX_HOME%\\lib\\cplex.jar"

            String script = sf.text
            templateBinding.forEach({ k, v -> script = script.replace("#$k#", v) })
            windowsScript.withWriter { it.write(script) }
            windowsScript.setExecutable(true, false)
        } else{
            delete(windowsScript)
        }
    }
}

distributions {
    main {
        contents {
            exclude 'gurobi-jar-*.jar'
            exclude 'cplex-*.jar'
            if (excludeNativeWin)
                exclude 'cbc-java-win-*.jar'
            if (excludeNativeLinux)
                exclude 'cbc-java-linux-*.jar'
            if (excludeNativeMac)
                exclude 'cbc-java-mac-*.jar'

            into('bin') {
                if (includeWinStarter) {
                    from("${buildDir}/launch4j") {
                        include("*.exe")
                    }
                }
                if (includeIxStarter) {
                    from("${rootDir}/scripts") {
                        include 'sirius-gui.sh'
                    }
                }

            }

            from("${rootDir}") {
                include('COPYING.txt')
                include('LICENSE.txt')
            }
            from("${buildDir}/reports/dependency-license"){
                include('licenses.md')
            }
        }
    }
}
distZip.dependsOn 'generateLicenseReport'
distZip.dependsOn('launch4j')
distZip.dependsOn('createGuiExe')
distTar.dependsOn('launch4j')
distTar.dependsOn 'generateLicenseReport'
distTar.dependsOn('createGuiExe')
installDist.dependsOn('launch4j')
installDist.dependsOn('createGuiExe')
installDist.dependsOn('generateLicenseReport')
distribution.dependsOn 'distZip'
//distribution.dependsOn 'distTar'
distribution.dependsOn 'createChecksums'
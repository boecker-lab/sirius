buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${spring_boot_version}")
    }
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'war'
apply plugin: 'java-library'

repositories {
    mavenLocal()
    mavenCentral()
    maven {//public libs only so anybody can build the frontend without login
        url "https://bio.informatik.uni-jena.de/repository/libs-oss"
        metadataSources {
            mavenPom()
            artifact()
        }
    }
    maven {
        url "https://www.ebi.ac.uk/Tools/maven/repos/content/groups/ebi-repo/"
    }
}

ext['jna.version'] = '5.4.0' //force correct jna version -> springs is not compatible with ilp solver
ext['slf4j.version'] = slf4j_version
ext['okhttp.version'] = '4.8.1'

dependencies {
    api project(':sirius_cli')
    api project(':sirius_gui')
    //implementation 'me.friwi:jcefmaven:116.0.19.1'

    //////////// configure logging ///////////////////
    //logging bridges
    implementation("org.slf4j:log4j-over-slf4j:$slf4j_version")
    implementation("org.slf4j:jcl-over-slf4j:$slf4j_version")
    implementation("org.slf4j:jul-to-slf4j:$slf4j_version")
    ////////////////////////////////////////////////////


    //////////// Spring (boot) ////////////////////////
    // Default bootJar config
    implementation("org.springframework.boot:spring-boot-starter-web"){
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }
//    implementation("org.springframework.boot:spring-boot-starter-jul")
//    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
    providedRuntime 'org.springframework.boot:spring-boot-starter-jetty'

    //spring data for pagination and sorting
    implementation 'org.springframework.data:spring-data-commons'

    // expose operational information about the running application via /actuator
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation 'it.unimi.dsi:fastutil:8.5.8' //replaces old spring version
    ////////////////////////////////////////////////////

    ////////////////////// API DOC ///////////////////
    // Generate interactive documentation from a Swagger-compliant API
    implementation 'org.springdoc:springdoc-openapi-ui:1.7.0'
    implementation 'org.springdoc:springdoc-openapi-javadoc:1.7.0'
    implementation "org.springdoc:springdoc-openapi-data-rest:1.7.0"
    //process javadoc for swagger/openapi
    annotationProcessor 'com.github.therapi:therapi-runtime-javadoc-scribe:0.13.0'
//    implementation 'org.springdoc:springdoc-openapi-security:1.5.13'
    ////////////////////////////////////////////////////

    //getter setter constructor logger, save boilerplate
    implementation 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

//    implementation 'io.micrometer:micrometer-core'
//    implementation 'io.micrometer:micrometer-registry-prometheus'
    //must match minio version
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'


    testImplementation group: 'junit', name: 'junit', version: '4.12'
}
//todo use logback instead of java util logging! => needs support in jjobs
configurations.each {Configuration conf ->
    //replaced by log4j-over-slf4j
    conf.exclude group: "log4j", module: "log4j"
    //replaced by jcl-over-slf4j
    conf.exclude group: "commons-logging", module: "commons-logging"
    conf.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
//    it.exclude group: 'org.slf4j', module: 'logback-classic'
    SiriusPlatform.allBut(siriusTargetPlatform).stream()
            .map { it.inchiClassifier() }.flatMap { it.stream() }
            .forEach { conf.exclude(group: "io.github.dan2097", module: "jna-inchi-$it") }
    if (excludeCbcNative)
        conf.exclude(group: "de.unijena.bioinf.cbc-java", module: "cbc-java-native")

    //remove commercial solver -> given at runtime
    conf.exclude group: "com.gurobi", module: "gurobi-jar"
    conf.exclude group: "cplex", module: "cplex"
//    conf.exclude group: "org.slf4j", module: "slf4j-jdk14"
}

war {
    enabled = false
}
jar {
    enabled = false
}

bootJar {
    classifier = 'boot'
    layered {
        enabled = true
    }
    launchScript()
}

artifactory {
    contextUrl = 'https://bio.informatik.uni-jena.de/repository/'
    publish {
        repository {
            if (version.toString().endsWith('-SNAPSHOT'))
                repoKey = 'libs-snapshot-local'
            else
                repoKey = 'libs-release-local'

            username = project.findProperty('de.unijena.bioinf.build.artifactory.username')
            password = project.findProperty('de.unijena.bioinf.build.artifactory.password')
        }
        defaults {
            publications('mavenJava')
        }
    }
}







